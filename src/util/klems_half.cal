{ RCSid $Id$ }
{
	Compute interior bin for half Klems BSDF basis (0-72)
	Returns -1 for wrong-side rays

	Modified from klems_full.cal

	7/18/2014	G. Ward
}
DEGREE` : PI/180;
mod`(n,d) : n - floor(n/d)*d;
Acos(x) : if(x-1, 0, if(-1-x, PI, acos(x))) / DEGREE;

khpola(r) : select(r, 6.5, 19.5, 32.5, 45.5, 58.5, 71.5, 90);
KHAPS : khpola(3) - khpola(2);
khnaz(r) : select(r, 1, 8, 12, 16, 20, 12, 8);
khaccum(r) : if(r-.5, khnaz(r) + khaccum(r-1), 0);
khfindrow(r, pol) : if(r-khpola(0)+.5, r,
		if(pol-khpola(r), khfindrow(r+1, pol), r) );

RHS = 1;	{ Set to -1 for left-handed system }
JTR = 0;	{ Sharp bin boundaries by default }
				{ Uniform perturbation function }
kjtr(n) = if(JTR, JTR*(.5 - rand(n*(9.54*Dx - 5.11*Dy + 1.37*Dz - 6.71))), 0);

kazn(azi,inc) = floor( mod(azi + inc*(.5+kjtr(2)), 360) / inc );

khbin2(pol,azi) =
	select(khfindrow(1, pol),
		kazn(azi,360/khnaz(1)),
		khaccum(1) + kazn(azi,360/khnaz(2)),
		khaccum(2) + kazn(azi,360/khnaz(3)),
		khaccum(3) + kazn(azi,360/khnaz(4)),
		khaccum(4) + kazn(azi,360/khnaz(5)),
		khaccum(5) + kazn(azi,360/khnaz(6)),
		khaccum(6) + kazn(azi,360/khnaz(7))
	);

khbin1(zv,yv,xv) = if(zv, khbin2(min(max(0, Acos(zv) + KHAPS*kjtr(1)), 90),
				atan2(yv,xv)/DEGREE),
			-1);

khbin(Nx,Ny,Nz,Ux,Uy,Uz) = khbin1(-Dx*Nx-Dy*Ny-Dz*Nz,
	-Dx*Ux-Dy*Uy-Dz*Uz + (Nx*Dx+Ny*Dy+Nz*Dz)*(Nx*Ux+Ny*Uy+Nz*Uz),
		-RHS*(Dx*(Uy*Nz-Uz*Ny) + Dy*(Uz*Nx-Ux*Nz) + Dz*(Ux*Ny-Uy*Nx)));
				
Nkhbins : khaccum(khnaz(0));	{ Number of bins for one elevation }
